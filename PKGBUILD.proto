# Contributor: Olivier Mehani <olivier.mehani@nicta.com.au>
#<!GIT
pkgname=oml2
_redmineid=940
#!GIT>
#<GIT
pkgname=oml2-git
#GIT>
pkgver=2.10.1
pkgrel=1
pkgdesc="OML is a measurement library that allows to define measurement points inside applications"
arch=(i686 x86_64)
url="http://oml.mytestbed.net/"
license=('custom:MIT-Nicta')
depends=('libxml2' 'sqlite3' 'popt' 'ruby>=1.8.7' 'postgresql-libs')
makedepends=('git' 'asciidoc')
optdepends=("postgresql: to use as a backend instead of sqlite3")
checkdepends=('check')
provides=(liboml liboml-git oml2)
conflicts=(liboml liboml-git oml2)
replaces=(liboml-git)
source=(
#<!GIT
	http://oml.mytestbed.net/attachments/download/${_redmineid}/oml2-${pkgver}.tar.gz
#!GIT>
	oml2-server.conf
	oml2-server.logrotate
	oml2-server.rc
	oml2-server.service
)

backup=(etc/conf.d/oml2-server.conf)
install="oml2.install"

#<!GIT
_builddir=${pkgname}-${pkgver}
#!GIT>
#<GIT
_gitroot="git://mytestbed.net/oml-staging.git"
_gitname="oml"
_gitbranch="master"
_builddir=${_gitname}-build
#GIT>

build() {
	cd "${srcdir}"
#<GIT
	msg "Connecting to GIT server...."
	if [ -d ${_gitname} ] ; then
		cd ${_gitname} && git pull origin
		msg "The local files are updated."
	else
		git clone ${_gitroot} ${_gitname}
	fi
	if [ ! -z ${_gitbranch} ]; then
		cd ${_gitname} && git checkout ${_gitbranch}
		cd ${_gitname} && git checkout master
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting make..."

	rm -rf "${srcdir}/${_builddir}"
	git clone "${srcdir}/${_gitname}" "${srcdir}/${_builddir}"
	cd "${srcdir}/${_builddir}"
	./autogen.sh
#GIT>
#<!GIT
	cd "${srcdir}/${_builddir}"
#!GIT>
	./configure --prefix=/usr --localstatedir=/var --enable-doc --disable-doxygen-doc --with-pgsql
	make || return 1
}

package() {
	cd "${srcdir}/${_builddir}"
	make DESTDIR="${pkgdir}/" install
	# XXX: We can only build the example application when we can find everything else
	make -C example/liboml2 \
		BINDIR="${pkgdir}/usr/bin" \
		SCAFFOLD="${pkgdir}/usr/bin/oml2-scaffold" \
		CFLAGS="-I${pkgdir}/usr/include" \
		LDFLAGS="-L${pkgdir}/usr/lib" \
		LIBS="-loml2 -locomm -lpopt -lm" \
		install
	mv ${pkgdir}/usr/bin/generator ${pkgdir}/usr/bin/oml2-generator
	install -D -m 0644 ${srcdir}/${_builddir}/COPYING \
		${pkgdir}/usr/share/licenses/${pkgname}/COPYING
	install -D -m 0755 ${startdir}/oml2-server.rc ${pkgdir}/etc/rc.d/oml2-server
	install -D -m 0644 ${startdir}/oml2-server.service ${pkgdir}/usr/lib/systemd/system/oml2-server.service
	install -D -m 0644 ${startdir}/oml2-server.conf ${pkgdir}/etc/conf.d/oml2-server.conf
	install -D -m 0644 ${startdir}/oml2-server.logrotate ${pkgdir}/etc/logrotate.d/oml2-server
}

check() {
	cd "${srcdir}/${_builddir}"
	make check
}
